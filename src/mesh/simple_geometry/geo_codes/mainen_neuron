// Spherical soma with holix transitions to cylindrilcal axon/dend

//     |-|   
//     |D|  
//     | |
//     / \   HD
//    /---\ 
//   / S   \
//   \     /
//    \---/
//     \ /   HA
//     | |
//     |A|
//     |-|
// z
// ^
// |
// |___>x
//
// Neuron
// Soma is a sphere at (0, 0, 0) with a radius rad_soma (surface marked as 1)
// Dendrite is a cylinder with length_dend, rad_dend (surface marked as 3)
// Axon is a cylinder with length_axon, rad_axon (surface marked as 2)
// Hilox to dendrite is wedge joining soma at rad_hilox_d. Its length
// is length_hilox_d (surfaces maked as 31)
// Hilox to axon is wedge joining soma at rad_hilox_a. Its length
// is length_hilox_a (surfaces maked as 21)
//
// Bbox
// Based on the geometry of neuron a bounding box is defined which is
// [-rad_soma-dxn, rad_soma+dxp] x [-rad_soma-dy, rad_soma+dy] x 
// [bottom of axon - dz, top of dend + dz]
//

soma = newv;
Sphere(soma) = {0, 0, 0, rad_soma}; //1

hilox_d = newv;
base_hilox_d = Sqrt(rad_soma*rad_soma-rad_hilox_d*rad_hilox_d);

If(Abs(rad_hilox_d - rad_dend) < 1E-10)
Cylinder(hilox_d) = {0, 0, base_hilox_d,
                     0, 0, length_hilox_d,
                     rad_dend}; //2
Else
Cone(hilox_d) = {0, 0, base_hilox_d,
                 0, 0, length_hilox_d,
                 rad_hilox_d, rad_dend}; //2
EndIf

dend = newv;
base_dend = base_hilox_d + length_hilox_d;
Cylinder(dend) = {0, 0, base_dend,
                  0, 0, length_dend, rad_dend, 2*Pi}; // 3

hilox_a = newv;
base_hilox_a = Sqrt(rad_soma*rad_soma-rad_hilox_a*rad_hilox_a);

If(Abs(rad_hilox_a - rad_axon) < 1E-10)
Cylinder(hilox_a) = {0, 0, -base_hilox_a,
                     0, 0, -length_hilox_a,
                     rad_axon}; //4
Else
Cone(hilox_a) = {0, 0, -base_hilox_a,
                 0, 0, -length_hilox_a,
                 rad_hilox_a, rad_axon}; //4
EndIf
               
axon = newv;         
base_axon = base_hilox_a + length_hilox_a;
Cylinder(axon) = {0, 0, -base_axon,
                  0, 0, -length_axon, rad_axon, 2*Pi};

neuron() = BooleanUnion{ Volume{soma}; Delete;}{ Volume{dend, hilox_d, axon, hilox_a}; Delete;};
neuron_surface() = Unique(Abs(Boundary{ Volume{neuron()}; }));

Physical Volume(1) = {neuron[]};
// Bake the surface into pieces
Physical Surface(3) = {neuron_surface[0], neuron_surface[1]}; // Dend
Physical Surface(31) = {neuron_surface[2]}; // Dend hilux
Physical Surface(1) = {neuron_surface[3]}; // Soma
Physical Surface(21) = {neuron_surface[4]}; // Axon hilux
Physical Surface(2) = {neuron_surface[5], neuron_surface[6]}; // Axon

// Bbox based on axon geometry
x0 = -rad_soma - dxn;
dx_box = rad_soma + dxp - x0;

y0 = -rad_soma - dy;
dy_box = 2*Abs(y0);

z0 = -base_axon - length_axon - dz;
z1 = base_dend + length_dend + dz;
dz_box = z1 - z0;

// Bounding box
bbox = newv;
Box(bbox) = {x0, y0, z0, dx_box, dy_box, dz_box};